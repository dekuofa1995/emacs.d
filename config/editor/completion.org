* Header
#+begin_src emacs-lisp
  ;;; init-completion.el -- Init File. -*- lexical-binding: t -*-
  ;;; Commentary:

#+end_src

* Completion
** Company and Cape Functions
#+begin_src emacs-lisp
	(defvar deku/capf-backend-alist
	  '((text-mode cape-dict)
	    (prog-mode cape-keyword))
	  "An alist matching modes to company backends.

	     The backends for any mode is built from this.")

	(defvar deku/global-capf-backends '(tempel-complete cape-dabbrev cape-file)
	  "An alist for global capf backends.")

	(cl-defun deku/update-capf-backends (modes &key cape company)
	  (if (and (null cape) (null company))
				;; as both backends are nil, clear backends for modes
	      (let ((modes (enlist modes)))
					(dolist (m modes)
						(setq deku/capf-backend-alist
									(delq (assq m deku/capf-backend-alist)
												deku/capf-backend-alist))))
	    (let* ((company (enlist company))
						 (cape (enlist cape))
						 (modes (enlist modes))
						 (comp-backend (if (null company)
															 (list)
														 (mapcar #'cape-company-to-capf company)))
						 (capfs (append cape comp-backend)))
				(dolist (m modes)
					(setf (alist-get m deku/capf-backend-alist)
								capfs)))))

	;;;###autoload
	(defun deku/capf-backends ()
	  "Get completion at point function backends."
	  (let (backends)
	    (let ((mode major-mode)
	          (modes (list major-mode)))
	      (while (setq mode (get mode 'derived-mode-parent))
	        (push mode modes))
	      (dolist (mode modes)
	        (dolist (backend  (cdr (assq mode deku/capf-backend-alist)))
	          (push backend backends)))
	      (delete-dups
	       (append (cl-loop for (mode . backends) in  deku/capf-backend-alist
	                        if (or (eq major-mode mode)  ; major modes
	                               (and (boundp mode)
																			(symbol-value mode))) ; minor modes
	                        append (append deku/global-capf-backends backends ))
	               (nreverse backends))))))

	;;;###autoload
	(defun deku/-setup-completions (capfs)
	  "Merge c into completion-at-point-functions."
	  (let ((capfs (enlist capfs)))
	    (dolist (c capfs)
	      (remove-hook 'completion-at-point-functions t t)
	      (add-hook 'completion-at-point-functions c 1 'local))))

	;;;###autoload
	(defun deku/update-capf ()
	  "Merge nasy capy."
	  (require 'cape)
	  (let ((backends (deku/capf-backends)))
	    (deku/-setup-completions backends)))


	;;;###autoload
	(defun nasy/orderless-dispatch-flex-first (_pattern index _total)
	  "orderless-flex for corfu."
	  (and (eq index 0) 'orderless-flex))


	;;;###autoload
	(defun nasy/setup-corfu ()
	  "Setup corfu."
	  (corfu-mode 1)
	  (setq-local orderless-matching-styles '(orderless-flex)
	              orderless-style-dispatchers nil)
	  (add-hook 'orderless-style-dispatchers #'nasy/orderless-dispatch-flex-first nil
	            'local))

#+end_src

*** Company Init
Just use =company= backends for =cape=.
#+begin_src emacs-lisp
  (setup company
    (:autoload company--multi-backend-adapter))
#+end_src

** Corfu
  +-----+------------------------+
  |Key  |Action                  |
  +-----+------------------------+
  |Tab  |corfu-complete          |
  +-----+------------------------+
  |C-n  |corfu-next              |
  +-----+------------------------+
  |C-p  |corfu-previous          |
  +-----+------------------------+
  |RET  |corfu-insert            |
  +-----+------------------------+
  |C-v  |corfu-scroll-up         |
  +-----+------------------------+
  |M-v  |corfu-scroll-down       |
  +-----+------------------------+
  |M-h  |corfu-info-documentation|
  +-----+------------------------+
  |C-M-i|completion-at-point     |
  +-----+------------------------+

#+begin_src emacs-lisp
  (setup corfu
    (:hooks (list prog-mode-hook
  		org-mode-hook
  		vterm-mode-hook
  		eval-expression-minibuffer-setup-hook)
  	    nasy/setup-corfu)
    ;; bindings
    ;; tab   corfu-complete
    ;; C-n/p corfu-next/previous
    ;; RET   corfu-insert
    ;; C-v   corfu-scroll-up
    ;; M-v   corfu-scroll-down
    ;; M-h   corfu-info-documentation
    (:global
     "M-/"   completion-at-point
     "C-M-i" complete-symbol)
    (:with-map corfu-map
      (:bind "C-g" corfu-quit
  	   "C-e" corfu-complete-common-or-next)
      (:unbind "<return>"))
    (:option*
     corfu-cycle t
     corfu-auto t
     corfu-preview-current nil
     corfu-auto-delay 0.4
     corfu-auto-prefix 2
     corfu-preview-current nil))
#+end_src

*** corfu-popupinfo
#+begin_src emacs-lisp
  (use-package corfu-popupinfo
    :custom
    (corfu-popupinfo-delay '(0.8 . 0.5))
    :hook (corfu-mode . corfu-popupinfo-mode))
#+end_src

** Cape
Completion At Point Extensions:
- =cape-dabbrev=: word from current buffers (see also =dabbrev-capf= on Emacs 29)
- =cape-file=: file name
- =cape-history=: from Eshell, Comint or minibuffer history
- =cape-keyword=: programming languages keyword
- =cape-symbol=: complete ~elisp~ symbol
- =cape-abbrev=: Complete abbreviation(=add-global-abbrev=, =add-mode-abbrev=)
- =cape-ispell=: Complete word from Ispell dictionay
- =cape-dict=: Complete word from dictionary file
- =cape-line=: Complete entire line from current buffer
- =cape-tex=: Complete Unicode char from TeX command, e.g. =\hbar=
- =cape-sgml=: Complete Unicode char from SGML entity, e.g. =&alpha=.
- =cape-rfc1345=: Complete Unicode char unsing RFC 1345 menemonics
*** TODO Setup Cape
- [ ] merge cape completion functions with company backends.
- [ ] add cape keybindings for specific CAPFs
#+begin_src emacs-lisp
  (setup cape
    (:once (list :before 'corfu-mode)
      (add-hook 'completion-at-point-functions #'cape-file))
    (:hooks corfu-mode-hook deku/update-capf)
    (:global
     "M-/" completion-at-point))
#+end_src

** TODO Orderless
*** Setup
#+begin_src emacs-lisp
  (setup orderless
    (:once (list :packages 'vertico)
      (require 'orderless))
    (:option* completion-styles '(prescient orderless))
    (:after 'consult
      (defun consult--orderless-regexp-compiler (input type &rest _config)
        (let
            (( input (orderless-pattern-compiler input)))
          (cons
           (mapcar (lambda (r) (consult--convert-regexp r type)) input)
           (lambda (str) (orderless--highlight input str)))))

      (defun consult--with-orderless (&rest args)
        (minibuffer-with-setup-hook
            (lambda ()
              (setq-local consult--regexp-compiler #'consult--orderless-regexp-compiler))
          (apply args)))
      ;; add
      (let
  	((override-commands '(consult-ripgrep consult-find)))
        (dolist (cmd override-commands)
  	(advice-add cmd :around #'consult--with-orderless)))))
#+end_src
*** COMMENT Older Setup
- [ ] clean orderless configuration.
#+begin_src emacs-lisp
  (use-package orderless
    :demand t
    :init
    (with-eval-after-load 'consult
      (require 'orderless)
      (defun +orderless--consult-suffix ()
        "Regexp which matches the end of string with Consult tofu support."
        (if (and (boundp 'consult--tofu-char) (boundp 'consult--tofu-range))
            (format "[%c-%c]*$"
                    consult--tofu-char
                    (+ consult--tofu-char consult--tofu-range -1))
          "$"))

      ;; Recognizes the following patterns:
      ;; * .ext (file extension)
      ;; * regexp$ (regexp matching at end)
      (defun +orderless-consult-dispatch (word _index _total)
        (cond
         ;; Ensure that $ works with Consult commands, which add disambiguation suffixes
         ((string-suffix-p "$" word)
          `(orderless-regexp . ,(concat (substring word 0 -1) (+orderless--consult-suffix))))
         ;; File extensions
         ((and (or minibuffer-completing-file-name
                   (derived-mode-p 'eshell-mode))
               (string-match-p "\\`\\.." word))
          `(orderless-regexp . ,(concat "\\." (substring word 1) (+orderless--consult-suffix))))))
      (orderless-define-completion-style +orderless-with-initialism
        (orderless-matching-styles '(orderless-initialism orderless-literal orderless-regexp)))
      (setq completion-styles '(orderless partial-completion basic)
            completion-category-defaults nil
          ;;; Enable partial-completion for files.
          ;;; Either give orderless precedence or partial-completion.
          ;;; Note that completion-category-overrides is not really an override,
          ;;; but rather prepended to the default completion-styles.
            ;; completion-category-overrides '((file (styles orderless partial-completion))) ;; orderless is tried first
            completion-category-overrides '((file (styles . (partial-completion))) ;; partial-completion is tried first
                                            ;; enable initialism by default for symbols
                                            (command (styles +orderless-with-initialism))
                                            (variable (styles +orderless-with-initialism))
                                            (symbol (styles +orderless-with-initialism)))
            orderless-component-separator #'orderless-escapable-split-on-space ;; allow escaping space with backslash!
            orderless-style-dispatchers (list #'+orderless-consult-dispatch
                                              #'orderless-affix-dispatch))))
#+end_src

* Footer
#+begin_src emacs-lisp
(provide 'init-completion)
;;; init-completion.el ends here
#+end_src
